; Configure Startup Script
; ------------------------
; Author: Henrik Noerfjand Stengaard
; Date: 2016-09-06
;
; This script configures of startup for Blizzard, ACA, Furia, CPU, FPU, Amiga Boot Selector and AGS2 for games and demos using yes/no dialogs. 


; Delete Workbench first time configure startup, if it exists
IF EXISTS SYS:WBStartup/Run-Configure-Startup
	delete >NIL: SYS:WBStartup/Run-Configure-Startup#?
EndIF


; Configure Startup Menu
; ----------------------
LAB menu

echo "Use Blizzard:       " NOLINE >T:Configure-Startup-Menu
IF EXISTS S:Use-Blizzard
	echo "Yes" NOLINE >>T:Configure-Startup-Menu
	IF EXISTS S:MapRom-Blizzard-A1200-KS31
		echo ", Map KS31" NOLINE >>T:Configure-Startup-Menu
	EndIF
	echo "" >>T:Configure-Startup-Menu
Else
	echo "No" >>T:Configure-Startup-Menu
EndIF

echo "Use ACA:            " NOLINE >>T:Configure-Startup-Menu
IF EXISTS S:Use-ACA
	echo "Yes" NOLINE >>T:Configure-Startup-Menu
	IF EXISTS S:MapRom-ACA-A600-KS31
		echo ", Map KS31" NOLINE >>T:Configure-Startup-Menu
	EndIF
	IF EXISTS S:MapRom-ACA-A1200-KS31
		echo ", Map KS31" NOLINE >>T:Configure-Startup-Menu
	EndIF
	echo "" >>T:Configure-Startup-Menu
Else
	echo "No" >>T:Configure-Startup-Menu
EndIF

echo "Use Furia:          " NOLINE >>T:Configure-Startup-Menu
IF EXISTS S:Use-Furia
	echo "Yes" NOLINE >>T:Configure-Startup-Menu
	IF EXISTS S:MapRom-Furia-A600-KS31
		echo ", Map KS31" NOLINE >>T:Configure-Startup-Menu
	EndIF
	echo "" >>T:Configure-Startup-Menu
Else
	echo "No" >>T:Configure-Startup-Menu
EndIF

echo "Use 020+ CPU:       " NOLINE >>T:Configure-Startup-Menu
IF EXISTS S:Use-020
	echo "Yes" >>T:Configure-Startup-Menu
Else
	echo "No" >>T:Configure-Startup-Menu
EndIF

echo "Use FPU:            " NOLINE >>T:Configure-Startup-Menu
IF EXISTS S:Use-FPU
	echo "Yes" >>T:Configure-Startup-Menu
Else
	echo "No" >>T:Configure-Startup-Menu
EndIF

echo "Start ABS:          " NOLINE >>T:Configure-Startup-Menu
IF EXISTS S:Start-ABS
	echo "Yes" >>T:Configure-Startup-Menu
Else
	echo "No" >>T:Configure-Startup-Menu
EndIF

echo "Start AGS2 Games:   " NOLINE >>T:Configure-Startup-Menu
IF EXISTS S:Start-AGS2Games
	echo "Yes" >>T:Configure-Startup-Menu
Else
	echo "No" >>T:Configure-Startup-Menu
EndIF

echo "Start AGS2 Demos:   " NOLINE >>T:Configure-Startup-Menu
IF EXISTS S:Start-AGS2Demos
	echo "Yes" >>T:Configure-Startup-Menu
Else
	echo "No" >>T:Configure-Startup-Menu
EndIF

echo "Configure WHDLoad" >>T:Configure-Startup-Menu
echo "Reset" >>T:Configure-Startup-Menu
echo "Reboot" >>T:Configure-Startup-Menu
echo "Exit" >>T:Configure-Startup-Menu


; Show configure startup menu
ReqList CLONERT BUTTONS I T:Configure-Startup-Menu O ENV:configurestartup H "Configure Startup"

; Switch Use Blizzard
IF $configurestartup eq "1" 
	SKIP blizzard
ENDIF 

; Switch Use ACA
IF $configurestartup eq "2" 
	SKIP aca
ENDIF 

; Switch Use Furia
IF $configurestartup eq "3" 
	SKIP furia
ENDIF 

; Switch Use 020+ CPU
IF $configurestartup eq "4" 
	SKIP cpu
ENDIF 

; Switch Use FPU
IF $configurestartup eq "5" 
	SKIP fpu
ENDIF 

; Switch Start ABS
IF $configurestartup eq "6" 
	SKIP abs
ENDIF 

; Switch Start AGS2 Games
IF $configurestartup eq "7" 
	SKIP ags2games
ENDIF 

; Switch Start AGS2 Demos
IF $configurestartup eq "8" 
	SKIP ags2demos
ENDIF 

; Configure WHDLoad
IF $configurestartup eq "9" 
	SKIP whdload
ENDIF 

; Reset
IF $configurestartup eq "10" 
	execute SYS:Programs/Configuration/Reset-Startup NOPROMPT
ENDIF 

; Reboot
IF $configurestartup eq "11"
	echo "Rebooting..."
	wait 1
	reboot
ENDIF 

SKIP end


; Configure Blizzard
LAB blizzard

IF EXISTS S:Use-Blizzard
	delete >NIL: S:Use-Blizzard
	delete >NIL: S:MapRom-Blizzard-A1200-KS31
Else
	echo "" > S:Use-Blizzard
	
	; Blizzard maprom A1200 Kickstart 3.1
	setenv blizzardmap `RequestChoice "Blizzard Maprom" "Do you want Blizzard to maprom A1200 Kickstart 3.1?" "Yes|No"`
	If $blizzardmap eq "1" 
		echo "" > S:MapRom-Blizzard-A1200-KS31
	Else
		; Delete "MapRom-Blizzard-A1200-KS31" file
		IF EXISTS S:MapRom-Blizzard-A1200-KS31
			delete >NIL: S:MapRom-Blizzard-A1200-KS31
		EndIF
	EndIF
EndIF

SKIP menu back


; Configure ACA
LAB aca

IF EXISTS S:Use-ACA
	delete >NIL: S:Use-ACA
	delete >NIL: S:MapRom-ACA-A600-KS31
	delete >NIL: S:MapRom-ACA-A1200-KS31
Else
	echo "" > S:Use-ACA

	; ACA maprom A600 Kickstart 3.1
	setenv acamaprom `RequestChoice "ACA Maprom" "Do you want ACA to maprom A600 Kickstart 3.1?" "Yes|No"`
	If $acamaprom eq "1" 
		echo "" > S:MapRom-ACA-A600-KS31
		
		SKIP menu back
	Else
		; Delete "MapRom-ACA-A600-KS31" file
		IF EXISTS S:MapRom-ACA-A600-KS31
			delete >NIL: S:MapRom-ACA-A600-KS31
		EndIF
	EndIF

	; ACA maprom A1200 Kickstart 3.1
	setenv acamaprom `RequestChoice "ACA Maprom" "Do you want ACA to maprom A1200 Kickstart 3.1?" "Yes|No"`
	If $acamaprom eq "1" 
		echo "" > S:MapRom-ACA-A1200-KS31
	Else
		; Delete "MapRom-ACA-A1200-KS31" file
		IF EXISTS S:MapRom-ACA-A1200-KS31
			delete >NIL: S:MapRom-ACA-A1200-KS31
		EndIF
	EndIF
EndIF

SKIP menu back


; Configure Furia
LAB furia

IF EXISTS S:Use-Furia
	delete >NIL: S:Use-Furia
	delete >NIL: S:MapRom-Furia-A600-KS31
Else
	echo "" > S:Use-Furia

	; Furia maprom A600 Kickstart 3.1
	setenv furiamaprom `RequestChoice "Furia Maprom" "Do you want Furia to maprom A600 Kickstart 3.1?" "Yes|No"`
	If $furiamaprom eq "1" 
		echo "" > S:MapRom-Furia-A600-KS31
	Else
		; Delete "MapRom-Furia-A600-KS31" file
		IF EXISTS S:MapRom-Furia-A600-KS31
			delete >NIL: S:MapRom-Furia-A600-KS31
		EndIF
	EndIF
EndIf

SKIP menu back
	

; Configure CPU
; -------------
LAB cpu

IF EXISTS S:Use-020
	delete >NIL: S:Use-020

	Copy >NIL: SYS:Programs/Configuration/KingCON1.3/Handler/KingCON-handler L:KingCon-handler
	Copy >NIL: SYS:Programs/Configuration/iconlib_46.4/icon.library_68000 Libs:icon.library
Else
	echo "" > S:Use-020

	Copy >NIL: SYS:Programs/Configuration/KingCON1.3/Handler/KingCON-handler.020 L:KingCon-handler
	Copy >NIL: SYS:Programs/Configuration/iconlib_46.4/icon.library_68020 Libs:icon.library
EndIF

SKIP menu back


; Configure FPU
; -------------
LAB fpu

IF EXISTS S:Use-FPU
	delete >NIL: S:Use-FPU

	Copy >NIL: SYS:Programs/Configuration/guigfx_render_nofpu/ Libs:
Else
	echo "" > S:Use-FPU

	Copy >NIL: SYS:Programs/Configuration/guigfx_render_fpu/ Libs:
EndIF

SKIP menu back


; Configure WHDLoad
; -----------------
LAB whdload

; Reset whdload.prefs
Copy >NIL: SYS:Programs/Configuration/WHDLoad/WHDLoad.prefs S:

; Select WHDLoad quit key
setenv quitkey `RequestChoice "WHDLoad Quit Key" "Select global quit key for WHDLoad?" "F10|Help|Del|NumL|ScrL|SysRq|PrtSc|Default"`
If $quitkey eq "1" 
	echo "QuitKey=$59" >> S:WHDLoad.prefs
EndIf
If $quitkey eq "2" 
	echo "QuitKey=$5f" >> S:WHDLoad.prefs
EndIf
If $quitkey eq "3" 
	echo "QuitKey=$46" >> S:WHDLoad.prefs
EndIf
If $quitkey eq "4" 
	echo "QuitKey=$5a" >> S:WHDLoad.prefs
EndIf
If $quitkey eq "5" 
	echo "QuitKey=$5b" >> S:WHDLoad.prefs
EndIf
If $quitkey eq "6" 
	echo "QuitKey=$5c" >> S:WHDLoad.prefs
EndIf
If $quitkey eq "7" 
	echo "QuitKey=$5d" >> S:WHDLoad.prefs
EndIf

SKIP menu back


; Configure Amiga Boot Selector
; -----------------------------
LAB abs

IF EXISTS S:Start-ABS
	delete >NIL: S:Start-ABS
Else
	echo "" > S:Start-ABS
	delete >NIL: S:Start-AGS2Games
	delete >NIL: S:Start-AGS2Demos
EndIF

SKIP menu back

	
; Configure AGS2 Games
; --------------------
LAB ags2games

IF EXISTS S:Start-AGS2Games
	delete >NIL: S:Start-AGS2Games
Else
	echo "" > S:Start-AGS2Games
	delete >NIL: S:Start-ABS
	delete >NIL: S:Start-AGS2Demos
EndIF

SKIP menu back

		
; Configure AGS2 Demos
; --------------------
LAB ags2demos

IF EXISTS S:Start-AGS2Demos
	delete >NIL: S:Start-AGS2Demos
Else
	echo "" > S:Start-AGS2Demos
	delete >NIL: S:Start-ABS
	delete >NIL: S:Start-AGS2Games
EndIF

SKIP menu back


; End
LAB end
