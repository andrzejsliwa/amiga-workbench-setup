; Configure Startup Script
; ------------------------
; Author: Henrik Noerfjand Stengaard
; Date: 2016-09-06
;
; This script configures of startup for Blizzard, ACA, Furia, CPU, FPU, Amiga Boot Selector and AGS2 for games and demos using yes/no dialogs. 


; Re-configure startup prompt, if not run configure startup exists in wbstartup
IF NOT EXISTS SYS:WBStartup/Run-Configure-Startup
	setenv configure `RequestChoice "Re-configure startup" "Do you want to re-configure startup configuration?" "Yes|No"`
	If $configure eq "0" 
		SKIP end
	EndIF
EndIF


; Reset startup without prompt
execute Reset-Startup NOPROMPT


; Use Accelerator
setenv useacc `RequestChoice "Use Accelerator" "Do you have an accelerator card installed?" "Yes|No"`
If $useacc eq "0" 
	SKIP cpufpu
EndIf


; Configure Blizzard
setenv blizzard `RequestChoice "Use Blizzard" "Do you have a Blizzard accelerator card installed?" "Yes|No"`
If $blizzard eq "1" 
	echo "" > S:Use-Blizzard

	; Blizzard maprom A1200 Kickstart 3.1
	setenv blizzardmap `RequestChoice "Blizzard Maprom" "Do you want Blizzard to maprom A1200 Kickstart 3.1?" "Yes|No"`
	If $blizzardmap eq "1" 
		echo "" > S:Use-Blizzard-A1200-KS31-MapRom
	Else
		; Delete "Use-Blizzard-A1200-KS31-MapRom" file
		IF EXISTS S:Use-Blizzard-A1200-KS31-MapRom
			delete >NIL: S:Use-Blizzard-A1200-KS31-MapRom
		EndIF
	EndIF

	SKIP cpufpu
Else
	; Delete "Use-Blizzard" file
	IF EXISTS S:Use-Blizzard
		delete >NIL: S:Use-Blizzard
	EndIF
EndIf


; Configure ACA
setenv useaca `RequestChoice "Use ACA" "Do you have an ACA accelerator card installed?" "Yes|No"`
If $useaca eq "1" 
	echo "" > S:Use-ACA

	; ACA maprom A600 Kickstart 3.1
	setenv acamaprom `RequestChoice "ACA Maprom" "Do you want ACA to maprom A600 Kickstart 3.1?" "Yes|No"`
	If $acamaprom eq "1" 
		echo "" > S:Use-ACA-A600-KS31-MapRom
		
		SKIP cpufpu
	Else
		; Delete "Use-ACA-A600-KS31-MapRom" file
		IF EXISTS S:Use-ACA-A600-KS31-MapRom
			delete >NIL: S:Use-ACA-A600-KS31-MapRom
		EndIF
	EndIF

	; ACA maprom A1200 Kickstart 3.1
	setenv acamaprom `RequestChoice "ACA Maprom" "Do you want ACA to maprom A1200 Kickstart 3.1?" "Yes|No"`
	If $acamaprom eq "1" 
		echo "" > S:Use-ACA-A1200-KS31-MapRom
	Else
		; Delete "Use-ACA-A1200-KS31-MapRom" file
		IF EXISTS S:Use-ACA-A1200-KS31-MapRom
			delete >NIL: S:Use-ACA-A1200-KS31-MapRom
		EndIF
	EndIF

	SKIP cpufpu
Else
	; Delete "Use-ACA" file
	IF EXISTS S:Use-ACA
		delete >NIL: S:Use-ACA
	EndIF
EndIf


; Configure Furia
setenv usefuria `RequestChoice "Use Furia" "Do you have a Furia A600 accelerator card installed?" "Yes|No"`
If $usefuria eq "1" 
	echo "" > S:Use-Furia

	; Furia maprom A600 Kickstart 3.1
	setenv furiamaprom `RequestChoice "Furia Maprom" "Do you want ACA to maprom A600 Kickstart 3.1?" "Yes|No"`
	If $furiamaprom eq "1" 
		echo "" > S:Use-Furia-A600-KS31-MapRom
	Else
		; Delete "Use-Furia-A600-KS31-MapRom" file
		IF EXISTS S:Use-Furia-A600-KS31-MapRom
			delete >NIL: S:Use-Furia-A600-KS31-MapRom
		EndIF
	EndIF
Else
	; Delete "Use-Furia" file
	IF EXISTS S:Use-Furia
		delete >NIL: S:Use-Furia
	EndIF
EndIf

		
; CPU & FPU
LAB cpufpu
	
	
; Configure CPU
setenv cpu `RequestChoice "Use 020" "Do you have a 020+ CPU installed?" "Yes|No"`
If $cpu eq "1" 
	echo "" > S:Use-020

	Copy >NIL: KingCON1.3/Handler/KingCON-handler.020 L:KingCon-handler
	Copy >NIL: iconlib_46.4/icon.library_68020 Libs:icon.library
Else
	; Delete "Use-020" file
	IF EXISTS S:Use-020
		delete >NIL: S:Use-020
	EndIF

	Copy >NIL: KingCON1.3/Handler/KingCON-handler L:KingCon-handler
	Copy >NIL: iconlib_46.4/icon.library_68000 Libs:icon.library
EndIf


; Configure FPU
setenv fpu `RequestChoice "Use FPU" "Do you have a FPU installed?" "Yes|No"`
If $fpu eq "1" 
	echo "" > S:Use-FPU

	Copy >NIL: guigfx_render_fpu/ Libs:
Else
	; Delete "Use-FPU" file
	IF EXISTS S:Use-FPU
		delete >NIL: S:Use-FPU
	EndIF

	Copy >NIL: guigfx_render_nofpu/ Libs:
EndIf


; Configure Amiga Boot Selector
setenv abs `RequestChoice "Start ABS" "Do you want start Amiga Boot Selector before loading Workbench?" "Yes|No"`
If $abs eq "1" 
	echo "" > S:ABS-Run

	SKIP end
Else
	; Delete "ABS-Run" file
	IF EXISTS S:ABS-Run
		delete >NIL: S:ABS-Run
	EndIF
EndIf

	
; Configure AGS2 Games
setenv ags2games `RequestChoice "Start AGS2 Games" "Do you want start AGS2 Games before loading Workbench?" "Yes|No"`
If $ags2games eq "1" 
	echo "" > S:AGS2Games-Run

	SKIP end
Else
	; Delete "AGS2Games-Run" file
	IF EXISTS S:AGS2Games-Run
		delete >NIL: S:AGS2Games-Run
	EndIF
EndIf

		
; Configure AGS2 Demos
setenv ags2demos `RequestChoice "Start AGS2 Demos" "Do you want start AGS2 Demos before loading Workbench?" "Yes|No"`
If $ags2demos eq "1" 
	echo "" > S:AGS2Demos-Run
Else
	; Delete "AGS2Demos-Run" file
	IF EXISTS S:AGS2Demos-Run
		delete >NIL: S:AGS2Demos-Run
	EndIF
EndIf


; End
LAB end


; Delete Workbench first time configure startup, if it exists
IF EXISTS SYS:WBStartup/Run-Configure-Startup
	delete >NIL: SYS:WBStartup/Run-Configure-Startup#?
EndIF
