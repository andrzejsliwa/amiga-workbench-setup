; Startup-Sequence
; ----------------


; Execute Blizzard script, if "Use-Blizzard" file exists
IF EXISTS S:Use-Blizzard
   execute S:Blizzard
ENDIF


; Execute ACA script, if "Use-ACA" file exists
IF EXISTS S:Use-ACA
   execute S:ACA
ENDIF


; Execute Furia script, if "Use-Furia" file exists
IF EXISTS S:Use-Furia
   execute S:Furia
ENDIF


; Load scsi.device and icon.library, if not "Blizzard-Run" file exists
IF NOT EXISTS S:Use-Blizzard
	C:LoadModule >NIL: DEVS:scsi.device LIBS:icon.library
ENDIF


C:SetPatch QUIET


C:Version >NIL:
C:AddBuffers >NIL: DF0: 15
FailAt 21


C:anticlick
C:PatchRam
C:MinStack 8192
C:TagLiFE
C:PatchWB


C:MakeDir RAM:T RAM:Clipboards RAM:ENV RAM:ENV/Sys
Assign ENV: RAM:Env
C:Copy ENVARC:~(#?.info) ENV: ALL QUIET


Resident >NIL: C:Assign PURE
Resident >NIL: C:Execute PURE

Assign >NIL: T: RAM:T
Assign >NIL: CLIPS: RAM:Clipboards
Assign >NIL: REXX: S:
Assign >NIL: PRINTERS: DEVS:Printers
Assign >NIL: KEYMAPS: DEVS:Keymaps
Assign >NIL: LOCALE: SYS:Locale
Assign >NIL: LIBS: SYS:Classes ADD
Assign >NIL: HELP: LOCALE:Help DEFER
Assign >NIL: DOPUS: SYS:Programs/DirOpus4


; User assign
execute S:User-Assign


; User env
execute S:User-Env


; Start Amiga Boot Selector, if "ABS-Run" file exists
IF EXISTS S:ABS-Run
	C:abs
	execute S:Boot.abs
	SKIP end
ENDIF


; Execute Arcade Game Selector 2 Games script, if "AGS2Games-Run" file exists
IF EXISTS S:AGS2Games-Run
	execute S:AGS2Games
ENDIF


; Execute Arcade Game Selector 2 Demos script, if "AGS2Demos-Run" file exists
IF EXISTS S:AGS2Demos-Run
	execute S:AGS2Demos
ENDIF


; Start workbench
execute S:WB


LAB end
